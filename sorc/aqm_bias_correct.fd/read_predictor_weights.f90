!------------------------------------------------------------------------------
!
! read_predictor_weights.f90 -- Read predictor weights from Netcdf file.
!
! This is a support routine for the NOAA NCO/ARL/PSD bias
! correction system for CMAQ forecast outputs.
!
! 2017-apr-29	Original version.  By Dave Allured, NOAA/CIRES/PSD.
! 2017-jul-11	Hard abort when weight file is missing or grossly defective,
!		  or when variables in file do not match requested variables.
!
! Notes:
!
! The predictor weight file is normally generated by a special
! test run of the bias correction program.  When invoked,
! subroutine write_predictor_weights.f90 writes the actual weight
! file.
!
! The special keyword "equal weights" in the file name will
! return equal weights for all sites and all predictor variables,
! without attempting to read a weight file.
!
! Equal predictor weights will be assigned for all requested site
! ID's that are not found in the weight file.
!
! If the caller's predictor variable names do not fully match the
! variable names in the weight file (except for ordering), then
! this routine will abort with a diagnostic message.
!
! Missing values in the weight array are not supported in any way.
! Given the several reversions to equal weights, missing values
! do not have any meaning in this context.
!
! File format:
!
! The predictor weight file is a simple Netcdf file containing
! only three variables:
!
!    site_ids(site)		-- Site ID's (character strings)
!    var_names(var)		-- Predictor variable names (char strings)
!    pred_weights(site,var)	-- Predictor weights
!
! There is one set of predictor weights for each site ID.
!
! Site ID's and variable names must be correctly aligned with
! the weight array on corresponding dimensions.  Aside from that,
! sites and var names may be in any order within the weight file.
!
! This routine will reorder the returned weight array to align
! with the caller's var name and site ID arrays.
!
! Predictor weights are stored normalized in the file, so that
! they sum to 1.0 for each site (within roundoff error).
!
!------------------------------------------------------------------------------

module read__predictor_weights
contains

subroutine read_predictor_weights (pred_weight_file, var_names, site_ids, &
      diag, pred_weights)

   use config, only : dp
   use read__netcdf_var
   use stdlit, only : normal
   use string_utils
   implicit none

! Calling arguments.

   character(*), intent(in) :: pred_weight_file	! name of weight file
   character(*), intent(in) :: var_names(:)	! requested predictor var names
   character(*), intent(in) :: site_ids(:)	! requested site ID's
   integer,      intent(in) :: diag		! verbosity level, 0-N

   real(dp), intent(out), allocatable :: pred_weights(:,:)   ! (VAR, SITE)

! Fixed variable names within Netcdf predictor weight file.

   character(*), parameter :: varname_sites   = 'site_ids'
   character(*), parameter :: varname_vars    = 'var_names'
   character(*), parameter :: varname_weights = 'pred_weights'

! Local variables.

   character(len(pred_weight_file)) filename2
   character varname*60

   integer nvars_file, nvars_out, nsites_file, nsites_out
   integer fi, oi, ncopy, nmatch, nmiss, status

   real(dp) equal_weights_value

   logical test1, test2

! Local arrays.

   character(2*len(site_ids)),  allocatable :: file_site_ids(:)	  ! (SITE)
   character(2*len(var_names)), allocatable :: file_varnames(:)	  ! (VAR)

   integer,  allocatable :: fvinds(:)			! (VAR)

   real(dp), allocatable :: file_weights(:,:)		! (VAR, SITE)

   logical,  allocatable :: cpflag(:)	 		! (SITE)

!-------------------------------------------------------------------
! Initialize.
!-------------------------------------------------------------------

   if (diag>=2) print *
   if (diag>=1) print *, 'read_predictor_weights: Read site-specific' &
                         // ' predictor weights file.'
   if (diag>=2) print '(2a)', '   Input file = ', trim (pred_weight_file)

   nsites_out = size (site_ids)		! caller's site and var dimensions
   nvars_out  = size (var_names)

! Default the output predictor weights array to all equal weights.

   allocate (pred_weights(nvars_out,nsites_out))   ! caller dims, not file dims

   equal_weights_value = 1.0_dp / nvars_out
   pred_weights(:,:) = equal_weights_value

! Check for equal weights option, special keyword in place of file name.

   filename2 = pred_weight_file
   call lowercase (filename2)		! compare case insensitive

   if (filename2 == 'equal weights') then
      print *, '  *** Equal weights are selected by special keyword.'
      print *, '  *** Defaulting to equal weights, all sites, all predictors.'

      return				! REVERT TO ALL EQUAL WEIGHTS
   end if

!-------------------------------------------------------------------
! Read whole variables from weight file, into conforming arrays.
! Use generic Netcdf reader.  Input arrays are auto-allocated.
!-------------------------------------------------------------------

   varname = varname_sites
   call read_netcdf_var    (pred_weight_file, varname, diag, file_site_ids, &
      status)

   if (status == normal) then
      varname = varname_vars
      call read_netcdf_var (pred_weight_file, varname, diag, file_varnames, &
         status)
   end if

   if (status == normal) then
      varname = varname_weights
      call read_netcdf_var (pred_weight_file, varname, diag, file_weights, &
         status)
   end if

! Check for read error.

   if (status /= normal) then
      print *
      print *, '*** read_predictor_weights: Abort, error reading predictor' &
         // ' weight file.'
      print *, '*** File = ' // trim (pred_weight_file)
      print *, '*** Var name = ' // trim (varname)
      call exit (1)			   ! hard error, abort run
   end if

   nsites_file = size (file_site_ids)	   ! site and var dimensions in file
   nvars_file  = size (file_varnames)

!-------------------------------------------------------------------
! Match all file variable names to requested variable names.
!-------------------------------------------------------------------

! This matching loop creates the permutation array from file var ordering
! to caller's requested var ordering.

   allocate (fvinds(nvars_out))
   fvinds(:) = 0

   do fi = 1, nvars_file
      do oi = 1, nvars_out
         if (file_varnames(fi) == var_names(oi)) then
            fvinds(oi) = fi		! exit inner block on first match;
            exit			! ensures a hole (no-match) for
         end if				!   a duplicate in either list
      end do
   end do

   test1 = (nvars_file /= nvars_out)	! check different number of variables
   test2 = any (fvinds(:) == 0)		! test for any not matched or duplicate

   if (test1 .or. test2) then
      print *
      print *, '*** read_predictor_weights: Abort, mismatched variable names.'
      print *, '*** Requested var names must be identical to var names in file,'
      print *, '***   except for ordering..'
      print *, '*** File = ' // trim (pred_weight_file)
      call exit (1)			! hard error, abort run
   end if

!-------------------------------------------------------------------
! Match all file site ID's to requested site ID's.
!-------------------------------------------------------------------

! This loop is different.  Copy predictor weights, site by site.
! For sites not matched, this reverts to equal weights for only those sites.

! In case of duplicate site ID's, the weights are assigned to sites on
! the basis of first match only.  However, counts to detect duplicates
! are gathered.

   allocate (cpflag(nsites_out))
   cpflag(:) = .false.
   ncopy     = 0

   do fi = 1, nsites_file	! match file site ID's to requested ID's...
      do oi = 1, nsites_out
         if (file_site_ids(fi) == site_ids(oi)) then
            pred_weights(:,oi) = file_weights(fvinds(:),fi)   ! (V,S) <-- (V,S)
				! copy weights for site, via permutation array
            cpflag(oi) = .true.	! mark output site as copied
            ncopy = ncopy + 1	! also count number of times copied
         end if
      end do
   end do

! Diagnostics.

   nmatch = count (cpflag)		! count of matched output sites
   nmiss  = count (.not.cpflag)		! count of non-matched output sites

   test1 = (nmatch /= ncopy)		! check consistency

   if (diag >= 2 .or. test1) then
      print '(a,i6)', '   Number of predictors in weight file    =', nvars_file
      print '(a,i6)', '   Number of sites in weight file         =', nsites_file
      print '(a,i6)', '   Number of requested sites              =', nsites_out
      print '(a,i6)', '   Number of requested sites with weights =', nmatch
      print '(a,i6)', '   Number of weight sets copied           =', ncopy
      print '(a,i6)', '   Number of req. sites missing weights   =', nmiss
   end if

   if (test1) then
      print *
      print *, '*** Count of sets copied is different than sites with weights.'
      print *, "*** Check for possible duplicate site ID's."
   end if

   if (diag >= 2) then
      print *
      print '(2(a,f0.3))', '   Min, max all weights in file = ', &
         minval (file_weights), ', ', maxval (file_weights)
   end if

   if (diag >= 3) print *, 'read_predictor_weights: Return.'

end subroutine read_predictor_weights
end module read__predictor_weights
