!------------------------------------------------------------------------------
!
! write_site_list.f90 -- Write Netcdf daily interpolated forecast files.
!
! This is a support routine for the NOAA NCO/ARL/PSD bias
! correction system for CMAQ forecast outputs.
!
! 2014-jun-20	Original version.  By Dave Allured.
!
! This routine creates a text file containing site ID's and
! coordinates for the given input sites.
!
! The output format is three text columns, plus a comment header.
! Sites are written in ascending order by site ID's.
!
! If a previous file exists by the same name, this version will
! print a warning message and not overwrite.
!
!------------------------------------------------------------------------------

module write__site_list
contains

subroutine write_site_list (outfile_template, date_index, hour, base_year, &
      calendar, title, site_ids, lats, lons, diag)

   use config, only : dp
   use expand__filename
   use index__sort
   use index_to_date_mod
   implicit none

! Input arguments.

   character(*), intent (in) :: outfile_template  ! output file name template
   integer,      intent (in) :: date_index	  ! date to embed in file name
   integer,      intent (in) :: hour		  ! hour num. to embed (eg. 12Z)
   integer,      intent (in) :: base_year	  ! base year for date indexes
   character(*), intent (in) :: calendar	  ! calendar system in use
   character(*), intent (in) :: title		  ! title line for output file
   character(*), intent (in) :: site_ids(:)	  ! site ID strings (S)
   real(dp),     intent (in) :: lats(:)		  ! site coordinates (S)
   real(dp),     intent (in) :: lons(:)
   integer,      intent (in) :: diag		  ! diag verbosity level, 0-N

! External function.

   integer get_free_unit

! Local variables.

   character outfile*200,  errmsg*200, fdate_str*24
   integer out, ios, j, si, nsites
   integer year, month, day
   logical ex

   integer sort_inds(size(site_ids))		! site indices for sorting

! Generate output file name from the template.

   if (diag >= 3) print *
   if (diag >= 3) print *, 'write_site_list: Start.'

   call index_to_date (date_index, year, month, day, base_year, calendar)
      					! get current Y M D integers

   call expand_filename (outfile_template, year, month, day, hour, outfile)
					! convert template to output file name

   if (diag >= 2) print '(2a)', ' Write site list: ', trim (outfile)

! Check for previous file.  Do not overwrite.

   inquire (file=outfile, exist=ex)

   if (ex) then
      print '(2a)', '*** write_site_list: Warning, file overwrite protect.'
      print '(2a)', '*** Previous site file exists with same name.'
      print '(2a)', '*** File = ', trim (outfile)
      print '(2a)', '*** Soft error, new site list file not written.'
      print *

      return				! soft error, return to caller
   end if

! Create new site file.

   out = get_free_unit ()		! allocate file unit number

   open (out, file=outfile, status='new', iostat=ios, iomsg=errmsg)

   if (ios /= 0) then
      print '(2a)',   '*** write_site_list: Warning:'
      print '(a,i0)', '*** Error opening new site file, iostat = ', ios
      print '(2a)',   '*** Iomsg = ', trim (errmsg)
      print '(2a)',   '*** File  = ', trim (outfile)
      print '(2a)',   '*** Soft error, new site list not written.'
      print *

      return				! soft error, return to caller
   end if

! Write comment header.

   write (out, '(2a)') trim (title)
   write (out, '(2a)') 'Generated by write_site_list.f90.'
   write (out, '(2a)') 'Part of interpolate_update.f90, the CMAQ bias', &
      ' correction program.'

   call fdate (fdate_str)	! get current date/time; fixed width string
   nsites = size (site_ids)

   write (out, '(2a)')
   write (out, '(2a)')   'Creation time   = ', trim (fdate_str)
   write (out, '(a,i0)') 'Number of sites = ', nsites

! Write column headers.
! Note that the line of dashes starting in column 1 is critical
! for later read subroutines.

   write (out, '(2a)')
   write (out, '(2a)') 'Site ID     Latitude   Longitude'
   write (out, '(2a)') '---------   --------   ---------'

! Sort in ascending order by site ID's.  Use index array.

   call index_sort (site_ids, sort_inds)

! Write site ID's and coordinates to output file.
! Note, site ID's are fixed width in character array.

   do j = 1, nsites
      si = sort_inds(j)
      write (out, '(a, f11.5, f12.5)') site_ids(si), lats(si), lons(si)
   end do

   close (out)

   if (diag >= 4) print *, 'write_site_list: File written.  Return.'
   if (diag >= 1) print *

end subroutine write_site_list
end module write__site_list
